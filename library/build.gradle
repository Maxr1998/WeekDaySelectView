apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// Version
def library_version = '1.0.1'
def library_name = 'weekdayselectview'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName library_version
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Kotlin stdlib
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.0.2'
}

def apiKey = null
def propFile = project.file("key.properties")
if (propFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    apiKey = props["key"]
}

// Maven & bintray config
version = library_version
publishing {
    publications {
        Production(MavenPublication) {
            groupId 'de.Maxr1998.android'
            artifactId library_name
            version library_version
            artifact("$buildDir/outputs/aar/library-release.aar")
            artifact sourcesJar
            artifact javadocsJar

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocsJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = 'maxr1998'
    key = apiKey
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = library_name
        websiteUrl = 'https://maxr1998.de'
        vcsUrl = 'https://github.com/Maxr1998/WeekDaySelectView.git'
        licenses = ['Apache-2.0']
        publish = true
        version {
            name = library_version
        }
    }
    publications = ['Production']
}